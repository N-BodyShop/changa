# Compiler-specific options for ChaNGa

# @synopsis COMPILER_FLAG_REQUIRE [flag]
# @summary Require compiler to support 'flag'                   
AC_DEFUN([COMPILER_FLAG_REQUIRE],
[dnl
	AC_MSG_CHECKING([if $CXX supports $1])
	AC_LANG_PUSH([C++])
	ac_saved_cxxflags="$CXXFLAGS"
	CXXFLAGS="-Werror $1"
	AC_COMPILE_IFELSE([AC_LANG_PROGRAM([])],
		[AC_MSG_RESULT([yes])],
		[AC_MSG_ERROR([$2])]
	)
	CXXFLAGS="$ac_saved_cxxflags"
	AC_LANG_POP([C++])
])

# @synopsis COMPILER_FLAG_CHECK [flag]
# @summary Check if compiler supports 'flag'                   
AC_DEFUN([COMPILER_FLAG_CHECK],
[dnl
	AC_MSG_CHECKING([for flag $1])
	AC_LANG_PUSH([C++])
	ac_saved_cxxflags="$CXXFLAGS"
	CXXFLAGS="-Werror $1"
	AC_COMPILE_IFELSE([AC_LANG_PROGRAM([])],
		[$2=$3],
		[$2=$4]
	)
	AC_MSG_RESULT([$$2])
	CXXFLAGS="$ac_saved_cxxflags"
	AC_LANG_POP([C++])
	AC_SUBST([$2])
])

# @synopsis COMPILER_MACRO_CHECK [compiler macro]
# @summary Check if compiler supports 'macro'
AC_DEFUN([COMPILER_MACRO_CHECK],
[dnl
	AC_LANG_PUSH([C++])
	AC_COMPILE_IFELSE([AC_LANG_PROGRAM(
		[[#ifndef $1
	  	  # error
	  	  #endif
		]])],
		[$2=yes],
		[$2=no]
	)
	AC_LANG_POP([C++])
])
